# Definiciones
# ============
define SPlural ["+Sing":0 | "+Plural":[%^ s]];
define OAGen ["+Masc":o | "+Fem":a];
define AGen ["+Masc":0 | "+Fem":[%^ a]];
define GenNum AGen SPlural;

define Cons [b|c|d|f|g|h|j|k|l|m|n|ñ|p|q|r|s|t|v|w|x|y|z];
define Vocal [a|e|i|o|u];

# Determinantes
# =============

# Articulos
# ---------
define Determinado "+Masc":{el} "+Sing":0
                 | "+Masc":{lo} "+Plural":s
                 | "+Neut":{lo} "+Sing":0
                 | "+Fem":{la} SPlural
                 ;
define Indeterminado "+Masc":{un} ["+Sing":0 | "+Plural":{os}]
                   | "+Fem":{una} SPlural
                   ;
define Articulo Determinado "+ArtD":0
              | Indeterminado "+ArtI":0
              ;

# Demostrativos
# -------------
define DDemostrativo 0:{est} [ ["+Masc" "+Sing"]:e
                          | "+Fem":a SPlural
                          | "+Neut":o
                          | "+Masc":o "+Plural":s
                          ] "+Dem":0;

# Numerales
# ---------
define Digito [%0|1|2|3|4|5|6|7|8|9];
define Numero [Digito* [(%.|%,) Digito+]*] "+Numeral":0;
define Ordinal {primer} [OAGen SPlural | "+Sing":0]
             | {tercer} [OAGen SPlural | "+Sing":0]
             | @txt"data/ES/OP/Num.txt" GenNum
             ;

define Determinante [ Articulo
                    | Numero
                    | DDemostrativo
                    | Ordinal "+Ord":0
                    ] "+Det":0
                  | ["DE+EL"]:{del}
                  | ["A+EL"]:{al}
                  ;

# Pronombres
# ==========

# Personales
# ----------
define Personal ["+1a":0 "+Sing":0 ["+Nom":{yo} | "+ObjD":{mí} | "+ObjI":{me}]]
              | ["+2a":0 "+Sing":0 ["+Nom":{tu} | "+ObjD":{tí} | "+ObjI":{te}]]
              | ["+3a":0 "+Masc":0 "+Sing":0 [0:{él} | "+Obj":{le} SPlural]]
              | ["+3a":0 "+Neut":0 "+Sing":0 [0:{ello} | "+Obj":{lo} SPlural]]
              | ["+3a":0 "+Fem":0 [0:{ella} | "+Obj":{la}] SPlural]
              | ["+1a":n | "+2a":v] 0:{osotr} OAGen "+Plural":s
              | ["+1a":n | "+2a":0] "+Obj":{os}
              | ["+3a" "+Masc" "+Plural"]:{ellos}
              ;
# Posesivos
# ---------
define Posesivo [ ["+1a" "+Sing"]:{mi} OAGen
                | ["+2a" "+Sing"]:{tu} (0:y OAGen)
                | "+3a":{su} (0:y OAGen)
                | ["+1a":n | "+2a":v] 0:{uestr} OAGen
                ] ("+PlurObj":s);


# Demostrativos
# -------------
define PDemostrativo 0:[[é|e] s t] [ ["+Masc" "+Sing"]:e
                          | "+Fem":a SPlural
                          | "+Neut":o
                          | "+Masc":o "+Plural":s
                          ] "+Dem":0;

define Pronombre [Personal "+Pers":0 | Posesivo "+Pos":0 | PDemostrativo] "+Pron":0;

# Variantes, Clase Abierta
# ========================

# Adjetivos
# ---------
define Adjetivo @txt"data/ES/OP/Adj.txt" GenNum "+Adj":0;

# Sustantivos
# -----------
define Sustantivo @txt"data/ES/OP/Sust.txt" GenNum "+Sust":0;

# Verbos
# ------
define VInfs @txt"data/ES/OP/Verb.txt";
define VConj ?* [ "@P.CONJ.1@":0 a
                | "@P.CONJ.2@":0 e
                | "@P.CONJ.3@":0 i
                ] r;
define VLex VConj .o. VInfs;

define Personas "+1s":0 | "+2s":s | "+3s":0
              | "+1p":[m o s] | "+2p":[%' i s] | "+3p":n
              ;

define NoFin "+Inf":0
           | "+Ger":[%R n d o]
           | "+Part":[%R d o] GenNum
           | "+Imp":%R ["+2s":0|"+2p":d]
           ;
define Futuro "+Fut":0
              [ "+1s":é | "+1p":{emos}
              | "+2s":{ás} | "+2p":{éis}
              | "+3s":{á} | "+3p":{án}
              ];
define Condicional "+Cond":{ía} Personas;
define SubjPret "+PretSubj":a Personas;

define PretI "+PretImperf":[%R b a] Personas "@E.CONJ.1@":0
           | "+PretImperf":[%R %V í a] Personas "@D.CONJ.1@":0
           ;
define PretP "+PretPerf":%R
               [ [ "+1s":[%V é] | "+1p":{mos}
                 | "+2s":{ste} | "+2p":{steis}
                 | "+3s":[%V ó] | "+3p":{ron}
                 ] "@E.CONJ.1@":0
               | [ "+1s":[%V í] | "+1p":[%V i m o s]
                 | "+2s":[%V i s t e] | "+2p":[%V i s t e i s]
                 | "+3s":[%V i ó ] | "+3p":[%V i e r o n]
                 ] "@D.CONJ.1@":0
               ];

define Pres "+Pres":%R [ "+1s":[%V o] | "+1p":{mos}
                       | "+2s":[%IE s] | "+2p":[%' i s]
                       | "+3s":%IE | "+3p":[%IE n]
                       ];

define VSuf NoFin
          | Futuro
          | Condicional
          | SubjPret
          | PretI
          | PretP
          | Pres
          ;

define Clitic "+Clit":[({se}) {le}|{la}|{lo} | {se}];

define Verbo VLex VSuf (Clitic) "+Verbo":0
   .o. r %R -> 0
   .o. Vocal %V -> 0
   .o. i %IE -> i e
   .o. a %' -> á
   .o. e %' -> é
   .o. i i -> i
   .o. i %' -> í
   .o. [%'|%IE] -> 0
   ;

# Lexicon
# =======
define Lexicon Determinante
             | Pronombre
             | Adjetivo
             | Sustantivo
             | Verbo
             ;

# Ortographic rules
# =================
regex Lexicon
  .o. [..] -> e || Cons _ %^ s
  .o. z -> c || _ %^ [e|i]
  .o. o -> 0 || _ %^ a
  .o. %^ -> 0;

eliminate flags
